/* Se especifica que fr, ft y fz son funciones de r, t y z */
/* y que r y t son funciones de x y de y */
depends([fr, ft, fz], [r, t, z])$
depends([r, t], [x, y])$

/* Se especifican "manualmente" las derivadas dr_dx, dr_dy, dt_dx, dt_dy */
gradef(r,x,  cos(t))$    gradef(r,y, sin(t))$    /* ecuaciones (*\eqref{eq:derrthetaxya}*) */
gradef(t,x, -sin(t)/r)$  gradef(t,y, cos(t)/r)$  /* ecuaciones (*\eqref{eq:derrthetaxyb}*) */

/* Se definen las funciones fx y fy, ecuaciones (*\eqref{eqs:FxFycil}*) */
fx : fr*cos(t) - ft*sin(t);
fy : fr*sin(t) + ft*cos(t);

/* F贸rmula del rotacional en coordenadas rectangulares ((*\ref{eq:rot_rectangulares}*)) */
rot_f_ijk : trigreduce([ diff(fz,y) - diff(fy,z), 
                         diff(fx,z) - diff(fz,x), 
                         diff(fy,x) - diff(fx,y)  ]);
                         
/* Se define la matriz de transformaci贸n T, ecuaci贸n (*\eqref{eq:matTcilindricas}*) */
T : matrix(
[ cos(t), -sin(t), 0 ],
[ sin(t),  cos(t), 0 ],
[      0,       0, 1 ]
)$

/* Se escribe la f贸rmula del rotacional en coordenadas rectangulares  */
/* y se convierte a la base especificada por los vectores rp, tp, zp  */
rot_f_rPtPzP : trigreduce(transpose(T).rot_f_ijk);
