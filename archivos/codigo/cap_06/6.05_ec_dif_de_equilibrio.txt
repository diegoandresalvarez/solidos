/* Se define la matriz de tensiones en coordenadas cilíndricas */
/* "sigma_cil", ecuación (*\eqref{eq:mattensionescil}*) */
sigma_cil : matrix(
[ sr,  trt, trz ],
[ trt, st,  ttz ],
[ trz, ttz, sz  ]
)$

/* Se define la matriz de transformación "T", ecuación (*\eqref{eq:matTcilindricas}*) */
T : matrix(
[ cos(t), -sin(t), 0 ],
[ sin(t),  cos(t), 0 ],
[      0,       0, 1 ]
)$

/* Se convierte la matriz de tensiones "sigma" de coordenadas cilíndricas */
/* a coordenadas rectangulares, ecuación (*\eqref{eq:tauTtauPTT}*) */
sigma: expand(T.sigma_cil.transpose(T))$

/* Se extraen los términos de la matriz de tensiones "sigma" en */
/* coordenadas rectangulares */
sx  : sigma[1][1];
sy  : sigma[2][2];
sz  : sigma[3][3];
txy : sigma[1][2];
txz : sigma[1][3];
tyz : sigma[2][3];

kill(sz)$ /* Se elimina la ecuación sz = sz */

/* Se especifica que sr, st, sz, trt, trz y ttz son funciones de r, t y z */
/* y que r y t son funciones de x y de y */
depends([sr, st, sz, trt, trz, ttz], [r, t, z])$
depends([r, t], [x, y])$

/* Se especifican "manualmente" las derivadas dr_dx, dr_dy, dt_dx, dt_dy */
gradef(r,x,  cos(t))$    gradef(r,y, sin(t))$    /* ecuaciones (*\eqref{eq:derrthetaxya}*) */
gradef(t,x, -sin(t)/r)$  gradef(t,y, cos(t)/r)$  /* ecuaciones (*\eqref{eq:derrthetaxyb}*) */

/* Vector de fuerzas másicas con respecto a la base {rP,tP,zP} */
bcil : matrix(
[ br ],
[ bt ],
[ bz ]
)$

/* Se expresa el vector de fuerzas másicas con respecto a la base {i,j,k} */
/* Recuerde que T está definida en las líneas 8 a 13, ecuación (*\eqref{eq:bTvbr}*) */
b : T.bcil$

/* Se hace la asignación respectiva de las fuerzas másicas */
X : b[1][1];
Y : b[2][1];
Z : b[3][1];

/* Las ecuaciones diferenciales de equilibrio "rectangulares", ec. (*\eqref{eq:eqdifequil3d}*) */
eq1 : diff(sx,  x) + diff(txy, y) + diff(txz, z) + X = 0$
eq2 : diff(txy, x) + diff(sy,  y) + diff(tyz, z) + Y = 0$
eq3 : diff(txz, x) + diff(tyz, y) + diff(sz,  z) + Z = 0$

/* Se toma el límite de las ecuaciones anteriores cuando "t tiende a cero" */
limit([eq1, eq2, eq3], t, 0);