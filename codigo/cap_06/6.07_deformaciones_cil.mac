/* Se especifica que ur, vt y w son funciones de r, t y z */
/* y que r y t son funciones de x y de y */
depends([ur, vt, w], [r, t, z])$
depends([r, t], [x, y])$

/* Se especifican "manualmente" las derivadas dr_dx, dr_dy, dt_dx, dt_dy */
gradef(r,x,  cos(t))$    gradef(r,y, sin(t))$    /* ecuaciones (*\eqref{eq:derrthetaxya}*) */
gradef(t,x, -sin(t)/r)$  gradef(t,y, cos(t)/r)$  /* ecuaciones (*\eqref{eq:derrthetaxyb}*) */

/* Se define la matriz de transformación T, ecuación (*\eqref{eq:matTcilindricas}*) */
T : matrix(
[ cos(t), -sin(t), 0 ],
[ sin(t),  cos(t), 0 ],
[      0,       0, 1 ]
)$

/* Deformaciones matemáticas en términos de deformaciones ingenieriles */
ert : grt/2$          erz : grz/2$          etz : gtz/2$

/* Se define la matriz de deformaciones en coord. cilíndricas, ec. (*\eqref{eq:matdefcil}*) */
def_cil : matrix(
[ er,  ert, erz ],
[ ert, et,  etz ],
[ erz, etz, ez  ]
)$

/* Se convierte dicha matriz de deformaciones a coordenadas rectangulares */
def: T.def_cil.transpose(T)$ /* ecuación (*\eqref{eq:defTdefprimaTT_cil}*) */

/* Se extraen los términos de la matriz de deformaciones en coordenadas */ 
/* rectangulares */
ex  : def[1][1]$      ey  : def[2][2]$      ez  : def[3][3]$
gxy : 2*def[1][2]$    gxz : 2*def[1][3]$    gyz : 2*def[2][3]$

/* Vector de desplazamientos con respecto a la base {rP,tP,zP} */
des_cil : matrix([ ur ], [ vt ], [ w ])$

/* Se expresa el vector de desplazamientos con respecto a la base {i,j,k} */
des: T.des_cil$

/* Se hace la asignación respectiva de los desplazamientos */
u : des[1][1]$
v : des[2][1]$

/* 
con las siguientes factorizaciones se observa que existen unas expresiones entre
paréntesis que deben igualarse a cero
*/
/*
facsum(ex  - diff(u, x) = 0,              sin(t), cos(t));
facsum(ey  - diff(v, y) = 0,              sin(t), cos(t));
facsum(ez  - diff(w, z) = 0,              sin(t), cos(t));
facsum(gxy - diff(u, y) + diff(v, x) = 0, sin(t), cos(t));
facsum(gyz - diff(v, z) + diff(w, y) = 0, sin(t), cos(t));
facsum(gxz - diff(u, z) + diff(w, x) = 0, sin(t), cos(t));
*/

/* Deformaciones en coordenadas rectangulares, ecuaciones (*\eqref{eq:deformaciones}*) y (*\eqref{eq:deformangul}*) */
eq1 : ex  = diff(u, x)$
eq2 : ey  = diff(v, y)$
eq3 : ez  = diff(w, z)$
eq4 : gxy = diff(u, y) + diff(v, x)$
eq5 : gyz = diff(v, z) + diff(w, y)$
eq6 : gxz = diff(u, z) + diff(w, x)$

/* Se hace sin(t)=0 y cos(t)=1 e imprimimos las deformaciones */
ev([eq1, eq2, eq3, eq4, eq5, eq6], sin(t)=0, cos(t)=1);
